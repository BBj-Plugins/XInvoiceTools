use org.apache.pdfbox.preflight.parser.PreflightParser

c! = Class.forName("org.apache.pdfbox.preflight.parser.PreflightParser",err=*next)
if c! = null() then
    classloader! = new com.basiscomponents.util.JarLoader(err=classpath_err)
    
    libdir$="XInvoiceTools/lib/"
    
    ch=unt
    open (ch)libdir$
    while 1
        read record (ch,end=*break)jar$
        if jar$(1,1)="." or pos(".jar"=jar$)=0 then continue
        classloader!.addFile(BBjAPI().getFileSystem().resolvePath(libdir$+jar$))
    wend
    
    c! = classloader!.loadClass("org.apache.pdfbox.preflight.parser.PreflightParser")
endif


wnd! = BBjAPI().openSysGui("X0").addWindow(10,10,640,480,"Validate PDF/A Document")

st! = wnd!.addStaticText(100,5,5,400,25,"File:")
ed_file! = wnd!.addEditBox(101,105,5,400,25,"")
ed_file!.focus()

btn_sel! = wnd!.addButton(102,510,5,25,25,"...")

btn_process! = wnd!.addButton(103,550,5,75,25,"Validate")
btn_process!.setEnabled(0)

ced! = wnd!.addCEdit(105,5,35,630,440,"",$0180$)

ed_file!.setCallback(BBjAPI.ON_EDIT_MODIFY,"onEdit")
wnd!.setCallback(BBjAPI.ON_CLOSE,"byebye")
btn_sel!.setCallback(BBjAPI.ON_BUTTON_PUSH,"selectfile")
btn_process!.setCallback(BBjAPI.ON_BUTTON_PUSH,"validate")
wnd!.setCallback(BBjAPI.ON_RESIZE,"resize")


process_events


byebye: 
    bye
    
onEdit:
    ev!= BBjAPI().getLastEvent()
    f$ = ev!.getText()
    
    btn_process!.setEnabled(0)
    if len(f$)>4 and cvs(f$(len(f$)-3,4),4)=".PDF" then
        ch=unt
        while 1
            open (ch,err=*break)f$
            btn_process!.setEnabled(1)
            close (ch)
            break
        wend
    fi
return
    
selectfile:
    f$=FILEOPEN("Select PDF File","/","","pdf","PDF Files"+$0a$+"*.pdf",0)
    if f$(1,2)="::" then return
    
    ed_file!.setText(f$)
    
    rem fall through...
    
validate:
    f$=ed_file!.getText()
    f$=BBjAPI().getFileSystem().resolvePath(f$,err=validation_error)
    text$="Validating "+f$+$0d0a$
    
    parser! = c!.getConstructor(String.class).newInstance(f$)
    parser!.parse()
    
    document! = parser!.getPreflightDocument()
    document!.validate()
    result! = document!.getResult()
    document!.close()
    if result!.isValid() then
        text$=text$+"This is a valid PDF/A-1b file."+$0d0a$
    else    
        text$=text$+"File is not a valid PDF/A-1b file. Errors:"+$0d0a$
        it! = result!.getErrorsList().iterator()
        while it!.hasNext()
            e! = it!.next()
            text$=text$+e!.getErrorCode() + " : " + e!.getDetails()+$0d0a$
        wend
    fi    
    
    ced!.setText(text$)
return

validation_error:
    ced!.setText("ERROR"+$0d0a$+errmes(-1))
return    

resize:
    ev!=BBjAPI().getLastEvent()
    ced!.setSize(ev!.getWidth()-10,ev!.getHeight()-40)
return


classpath_err:
        PRINT "Add the preflight....jar files in the lib subfolder of this plugin to your classpath or update to a recent 19.20ff BBj version so they can be loaded dynamically!",252
        input *
        release
    